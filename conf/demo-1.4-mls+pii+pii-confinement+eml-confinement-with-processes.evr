script demo

// For MLS
when any user performs "Object read"
    on any object of attribute TS
  do deny process process_default()
    operation "File write" on
      complement of attribute TS

when any user performs "Object read"
    on any object of attribute S
  do deny process process_default()
    operation "File write" on
      complement of attribute S_TS

// For PII policy.
when any user of attribute Doctor performs "Object read"
     on any object of any record
  do
    assign object attribute oattr_record_of_default_obj()
      to object attribute Today
    create rule rule_composed_of(
     "when process #1 performs \"Object write\" on any object do assign object attribute oattr_of_default_obj() to object attribute \"Today\"", id_or_name_as_string(process_default()))


when any user performs "Object read"
     on any object of attribute Today
  do
    create rule rule_composed_of(
     "when session #1 performs \"Object write\" on any object do assign object attribute oattr_of_default_obj() to object attribute \"Today\"", id_or_name_as_string(process_default()))


// For confinement of PII data.
when any user performs "Object read"
     on any object of attribute "CMR Columns"
  do
    deny process process_default() operation "File write" on
      complement of attribute "CMR Columns"

// For email confinement.
when any user performs "Object create" on any object of "OU messages"
do
  create object obj_rep_of_oattr(oattr_of_default_obj())
    representing object attribute oattr_of_default_obj() in
    object attribute "OU messages"

  grant user attribute uattr_name_of_user(user_default()) operation
       "Object attribute assign" on object attribute
       oattr_rep_of_oattr(oattr_of_default_obj())

when any user performs "Object read"
     on any object of attribute "OU messages"
do
  deny process process_default() operation "File write" on complement
    of attribute "OU messages"

  deny process process_default() operation "Object attribute assign to"
    on intersection of complement of attribute "OU inboxes",
    complement of attribute "OU outboxes"
